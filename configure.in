dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.52)
AC_INIT(ChangeLog)

dnl * 
dnl * Release version.
dnl * 
SARY_MAJOR_VERSION=1
SARY_MINOR_VERSION=2
SARY_MICRO_VERSION=0
SARY_VERSION=$SARY_MAJOR_VERSION.$SARY_MINOR_VERSION.$SARY_MICRO_VERSION
VERSION=$SARY_VERSION

AC_SUBST(SARY_MAJOR_VERSION)
AC_SUBST(SARY_MINOR_VERSION)
AC_SUBST(SARY_MICRO_VERSION)

AM_INIT_AUTOMAKE(sary, $VERSION)
AM_MAINTAINER_MODE

dnl *
dnl * Copyright.
dnl * 
COPYRIGHT="Copyright (C) 2000-2002 Satoru Takabayashi All rights reserved."
AC_SUBST(COPYRIGHT)
AC_DEFINE_UNQUOTED(COPYRIGHT, "$COPYRIGHT", [Copyright])

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

AC_ISC_POSIX

dnl *
dnl * libtool
dnl *
AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL

# Before making a release, the LTVERSION string should be modified.
# The string is of the form C:R:A.
# - If interfaces have been changed or added, but binary compatibility has
#   been preserved, change to C+1:0:A+1
# - If binary compatibility has been broken (eg removed or changed interfaces)
#   change to C+1:0:0
# - If the interface is the same as the previous version, change to C:R+1:A

LTVERSION="10:0:0"
AC_SUBST(LTVERSION)

dnl *
dnl * GCC
dnl *

if test -n "$GCC"; then
    CFLAGS="$CFLAGS -Wall -Wunused -Wuninitialized -Wmissing-prototypes -Wmissing-declarations"
fi


dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

dnl Checks for library functions.
AC_FUNC_MMAP

dnl *
dnl * GLIB
dnl *
AM_PATH_GLIB_2_0(2.0.0,,
  AC_MSG_ERROR([
*** GLIB 2.0.0 or better is required. The latest version of GLIB
*** is always available from ftp://ftp.gtk.org/.]),
    gobject gmodule)

GLIB_CFLAGS=`$PKG_CONFIG --cflags gthread-2.0`
GLIB_LIBS=`$PKG_CONFIG --libs gthread-2.0`
AC_SUBST(GLIB_LIBS)
AC_SUBST(GLIB_CFLAGS)

AM_CONFIG_HEADER(config.h)
AC_OUTPUT([	Makefile 
		debian/Makefile
		tests/Makefile
		docs/Makefile
		docs/en/Makefile
		docs/ja/Makefile
		docs/images/Makefile
		man/Makefile
		src/Makefile
		sary/Makefile
		sary/version.c
		sary/saryconfig.h
		sary.spec
		sary.pc])
